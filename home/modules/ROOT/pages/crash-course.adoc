= Crash course
:title: Crash Course
:test-tql: custom
:test-tql-entry: schema_start
:test-python: yes
:test-rust: yes
:keywords: typedb, typeql, clients, documentation, overview
:pageTitle: Crash course
:page-aliases: {page-version}@home::crash-course/overview.adoc
:summary: A 15-minute crash course on using TypeDB

TypeDB brings together powerful programming concepts with the features and performance of modern database systems. We believe the result is a novel database experience - and after taking this 15-minute crash course, we hope you'll agree.

== Storing data in types

In TypeDB, data is stored in *types*, which can be either

* *entity* types (e.g., a type of all the ``user``s in your app)
* *relation* types (e.g., all the ``friendship``s of users)
* *attribute* types (e.g., the ``username``s of ``user``s, or the ``start_date``s of ``friendship``s)

Data objects (instances) may reference other instances. A relation references other entities or relations, which we call its role players. An attribute is owned by a single entity or relation, which we call its owner. Makes sense, right?

TypeDB's type system unifies concepts from relational, document, and graph DBMSs, blending them with a modern programming paradigm. Let's see how this plays out for writing queries.

[[read]]
== "`Query programming`" with TypeDB

The query language of TypeDB is *TypeQL*. It combines declarative and functional programming concepts with the simplicity of natural language. Here's a simple TypeQL query to get us started:

//!program[lang=tql, type=read, name=read_start]
//!++
[,typeql]
----
match
  $alex isa user, has username "grothendieck_25";
  $friendship isa friendship (friend: $alex, friend: $friend-of-alex);
reduce $alex-friend-count = count;
----
//!--
//!run

This query returns the count of how many friends the user `$alex` has (identified by their username `"grothendieck_25"`). Note that the query works in two stages:

1. First, the `match` stage outputs all answers from the database that satisfy the statements in the body of the stage. This means we output all possible data combinations from the database for the three used variables!

2. Second, the `reduce` stage counts these answer combinations, outputs a single number, and assigns it to the variable `$alex-friend-count`.

[NOTE]
====
The pattern `$var isa <type>` means the variable `$var` represents a data object (instance) of type `<type>`. An instance may be independent, or it may reference other instances.

TypeQL has special statements to capture references between instances, like `$relation isa <type> (<role>: $player);` or `$owner has <type> $attribute`.
====

Here's an example of a slightly more complex query.

//!program[lang=tql, type=read]
//!++
[,typeql]
----
match
  $alex isa user, has username "grothendieck_25";
  friendship (friend: $alex, friend: $friend-of-alex); # can omit variable
reduce $alex-friend-count = count;
match
  $other-user isa user, has username $other-name;
  friendship (friend: $other-user, friend: $friend-of-other-user);
reduce $other-friend-count = count
  groupby $other-name, $alex-friend-count;
match
  $alex-friend-count < $other-friend-count;
select $other-name;
----
//!--
//!run

The goal of this query is to find users with more friends than Alex. It has 6 stages:

* *Stage 1 and 2: Count Alex' friends* - and assign the output to ``$alex-friend-count``.
* *Stage 3 and 4: Count all other users' friends*. Match all ``$other-user``s with their respective usernames, ``$other-name``, and their friends. Group the answers by unique combinations of `$other-name` and `$alex-friend-count`. Finally, ``count`` them.
* *Stage 5 and 6: List users with more friends than Alex*. Introduce a condition - `$alex-friend-count < $other-friend-count` - and return only the results that satisfy it. `select` the username: `$other-name`.

Isn't that neat? Combining multiple query stages into a single query pipeline is powerful. However, the resulting query now has quite a bit of repetitive code. Let's optimize it using a helper function.

(Don't worry. This is not an exercise for the reader. We've optimized it for you.)

//!program[lang=tql, type=read]
//!++
[,typeql]
----
with fun friend_count($user: user) -> integer:
  match friendship (friend: $user, friend: $friend-of-user);
  return count;
match
  $alex isa user, has username "grothendieck_25";
  $other-user isa user, has username $other-name;
  friend_count($alex) < friend_count($other-user);
select $other-name;
----
//!--
//!run

We've just defined a helper function using the `fun` keyword that abstracts a key part of our query logic. Functions use the same syntax as queries, so they're easy to write.

[[schema]]
== Defining your type schema

The types `user` and `username` in the above queries don't come out of nowhere: in order to query data in types, you first need to define them. Types are defined with `define` queries like so:

//!program[lang=tql, type=schema, name=schema_start]
//!++
[,typeql]
----
define
  entity user, owns username, owns status;
  attribute username, value string;
  attribute status, value string;
----
//!--

Similarly, if we were to add a `friendship` type to our schema we would define:

//!++
[,typeql]
----
define
  relation friendship, relates friend @card(2);
  user plays friendship:friend;
----
//!--

* *Line 1*: Introduce the relation type `friendship` with a `friend` *_role_* (programmer-lingo: think "`trait`" that can then be "`implemented`" by other types). Declare that each friendship requires exactly two friends (`card` is short for "`cardinality`", and is a first example of a *annotation*: annotations make schema statements more specific - more on that later).
* *Line 2*: Users can play the role of friends in friendships (more programmer-lingo: the type `user` implements the "`friend-role`" trait).

Relation types, like entity types, are types of "`first-class objects`" in our type system. In particular, they too can have attributes (or may play roles in other relation types).

//!++
[,typeql]
----
define
  attribute start-date, value datetime;
  friendship owns start-date @card(0..1);
----
//!--

This defines a `start-date` attribute and then declares each friendship may own a `start_date` (yet more programmer-lingo: the type `friendship` implements the "`start-date-owner`" trait). Moreover, the `@card` annotation specifies that friendships own between zero or one start dates. This is the default cardinality, so you don't actually need `@card` here at all.

The next example illustrates usage of "`unbounded`" cardinality together with another useful annotation, `@key` (which, in this case, specifies that the username of a user should uniquely identify a user, i.e., it is a _key attribute_).

//!++
[,typeql]
----
define
  user owns status @card(0..);  # as many status attributes as you wish
  user owns username @key;
----
//!--

Importantly, different types may own the same attributes; and, similarly, different types may play the same role of a relation type. This flexibility of connecting types is a central feature of data modeling with TypeDB.

//!++
[,typeql]
----
define
  entity organization,
    owns username,
    owns status,
    plays friendship:friend;
----
//!--

Finally, yet another fundamental and powerful feature of TypeDB's type system is *subtyping*: analogous to modern programming language features, this gives us the ability to organize our types hierarchically:

//!++
[,typeql]
----
define
  entity company sub organization;    # subtypes will inherit ownership and role
  entity university sub organization; # playing traits from their supertype
----
//!--
//!run

TypeDB's type system re-thinks data models from first principles: it modularizes schemas into their "atomic" components. For example, you can add or remove ownership at any point in time, or edit specific annotations. This makes it easy to migrate and combine data, and programmatically re-structure your database if necessary. There is much more to explore, but we refer to the xref:{page-version}@manual::schema/index.adoc[Schema Manual] for further details.

[[CRUD]]
== CRUD operations

Having defined the types in our schema, we are ready to write data to our database. To begin, let's insert a user with the following `insert` query:

//!program[lang=tql, type=write, fail_at=commit]
//!++
[,typeql]
----
insert $x isa user;
----
//!--
//!run

This works, but is pretty useless: it inserts an entity with type `user`, but gives no information about that entity (in fact, the query will fail if `user owns username @key`).

The following is more meaningful:

//!program[lang=tql, type=write]
//!++
[,typeql]
----
insert $x isa user, has username "user_0";
----
//!--
//!run

We can insert multiple values in a single `insert` query like so:

//!program[lang=tql, type=write]
//!++
[,typeql]
----
insert
  $x isa user, has username "user_1";
  $y isa user;
  $y has username "user_2";  # we can separate `isa` and `has`
----
//!--
//!run

This inserts two users at the same time. Recall, that we set usernames to be a `@key` attribute of our users, so you would - rightfully - expect the following query to fail:

//!program[lang=tql, type=write, fail_at=runtime]
//!++
[,typeql]
----
insert
  $x isa user, has username "user_3";
  $y isa user, has username "user_3";
----
//!--
//!run

And it does fail, because the key 'user_3' is now claimed by two distinct `user` instances.

So far so good. Next question: how do we insert friendships? We need to refer to users to instantiate our friendship (recall: each friendship takes two friends). That's where variables come into play.

//!program[lang=tql, type=write, jump=read_start]
//!++
[,typeql]
----
insert
  $x isa user, has username "grothendieck_25";
  $y isa user, has username "hartshorne";
  friendship (friend: $x, friend: $y); # alternatively: $z isa friendship (friend: $x, friend: $y);
----
//!--

And now we've inserted two new users with a `friendship` relation between them.

What if we want to create a `friendship` relation between two existing users already in the database? That's a two-stage query pipeline - (1) retrieve them with `match`; (2) `insert` the new `friendship`.

//!++
[,typeql]
----
match
  $u0 isa user, has username "user_0";
  $u1 isa user, has username "user_1";
  $u2 isa user, has username "user_2";
insert
  friendship (friend: $u0, friend: $u1);
  friendship (friend: $u0, friend: $u2);
  friendship (friend: $u1, friend: $u2);
----
//!--

Easy!

Deleting and updating data work in a similar vein: all data write operations can be used as pipeline stages, and they all use essentially the same declarative statement-by-statement syntax. For example, the following pipeline adds "VIP" to a user's status, then traverses all friendships of that user, and marks friends as VIPs themselves if they now have more than five VIP friends.

//!++
[,typeql]
----
match
  $user isa user, has username "user_0";
insert $user has status "VIP";
match
  friendship (friend: $user, friend: $friend);
  friendship (friend: $friend, friend: $friend-of-friend);
  $friend-of-friend has status "VIP";
reduce $VIP-friend-count = count groupby $friend;
match $VIP-friend-count > 5;
insert $friend has status "VIP";
----
//!--
//!run

See the xref:{page-version}@manual::CRUD/index.adoc[CRUD manual] for more.

== Database management 101

The time has come to actually run our queries. Recall from the xref:{page-version}@home::quickstart.adoc[Quickstart], the first two steps of the TypeQB workflow are always as follows:

1. Ensure that your xref:{page-version}@home::quickstart.adoc#_create_free_cluster[**TypeDB cluster**] is running.
1. From the **TypeDB client** of your choice, establish a xref:{page-version}@home::quickstart.adoc#_connect_cluster[connection] to the server.

Next up, let us create a database. The workflow here will depend on your client, but is easy in either case.

[tabs]
====
Console::
+
--
To list existing databases use:
----
$ database list
----
To create a new database called `my-db` use
----
$ database create my-db
----
To delete a database use
----
$ database delete my-db
----
--

Python::
+
--
//!program[lang=python]
////
from typedb.driver import TypeDB, TransactionType, Credentials, DriverOptions
user = "admin"
pw = "password"
address = "127.0.0.1:1729"
database = "test"
////
After connecting with
//!++
[,python]
----
driver = TypeDB.core_driver(address=address, credentials=Credentials(user,pw), driver_options=DriverOptions())
----
//!--
you can create a database with:
//!++
[,python]
----
driver.databases.create(database)
----
//!--
and delete a database with:
//!++
[,python]
----
driver.databases.get(database).delete()
----
//!--
//!run
--

Rust::
+
--
//!program[lang=rust]
////
use typedb_driver::{Credentials, DriverOptions, TransactionType, TypeDBDriver};
use std::error::Error;

#[tokio::main]
async fn main() -> Result<(), Box<dyn Error>> {
    let address = "127.0.0.1:1729";
    let user = "admin";
    let pw = "password";
    let db_name = "test";
////
After connecting with
//!++
[,rust]
----
let driver = TypeDBDriver::new_core(address, Credentials::new(user, pw), DriverOptions::new(false, None)?).await?;
----
//!--
you can create a database with:
//!++
[,rust]
----
driver.databases().create(db_name).await?;
----
//!--
and delete a database with:
//!++
[,rust]
----
driver.databases().get(db_name).await?.delete().await?;
----
//!--
////
    Ok(())
}
////
//!run
--

Studio::
+
--
After opening a project, use the database management button (image:{page-component-version}@home::studio-icons/svg/studio_dbs.svg[width=24]) to create and manage databases.

See the xref:{page-version}@manual::tools/studio.adoc[Studio Manual] for more.
--
====

== Running queries with transactions

In order to run our queries, the next step is to **open a transaction** for an existing database. We can run multiple queries in single a transaction, and then commit the transaction to persist our changes when done. Transactions are required in TypeDB - you can't run a query without one. There are three types of transactions:

* *Schema* transactions allow you to run schema definition queries (like the `define` queries that we've seen above).
* *Write* transactions allow you to send query pipelines that may write data (i.e., they contain "`write stages`" like the `insert` or `delete` stages that we have seen above)
* *Read* transactions allow you to send query pipelines that only read data (i.e., they contain only "`read stages`" like the `match`, `reduce`, or `select` stages that we have seen above).

How to open a transaction and run a query will depend on your client.

[tabs]
====
Console::
+
--
Open the transaction with
----
transaction my-db <TYPE>
----
where `<TYPE>` can be one of `schema`, `write`, or `read`.

Then run your queries in the newly opened prompt. Use
----
commit
----
if you want to commit changes.
--

Python::
+
--
//!program[lang=python]
////
from typedb.driver import TypeDB, TransactionType, Credentials, DriverOptions
user = "admin"
pw = "password"
address = "127.0.0.1:1729"
database = "test"
driver = TypeDB.core_driver(address=address, credentials=Credentials(user,pw), driver_options=DriverOptions())
driver.databases.create(database)
with driver.transaction(database, TransactionType.SCHEMA) as tx:
    response = tx.query("define entity something;").resolve()
    tx.commit()
////
//!run
Open a transaction and send a query with
[,python]
----
with driver.transaction(database, TransactionType.<TYPE>) as tx:
    response = tx.query(<QUERY>).resolve()
----
where `<TYPE>` can be one of `SCHEMA`, `WRITE`, or `READ`. Use
[,python]
----
    tx.commit()
----
if you want to commit your changes.
--

Rust::
+
--
//!program[lang=rust]
////
use typedb_driver::{Credentials, DriverOptions, TransactionType, TypeDBDriver};
use std::error::Error;

#[tokio::main]
async fn main() -> Result<(), Box<dyn Error>> {
let address = "127.0.0.1:1729";
let user = "admin";
let pw = "password";
let db_name = "test";
let driver = TypeDBDriver::new_core(address, Credentials::new(user, pw), DriverOptions::new(false, None)?).await?;
driver.databases().create(db_name).await?;
let tx = driver.transaction(db_name, TransactionType::Schema).await?;
tx.query("define entity something;").await?;
tx.commit().await?;
Ok(())
}
////
//!run
Open a transaction and send a query with
[,rust]
----
let tx = driver.transaction(db_name, TransactionType::<TYPE>).await?;
tx.query(<QUERY>).await?;
----
where `<TYPE>` can be one of `SCHEMA`, `WRITE`, or `READ`. Use
[,rust]
----
tx.commit().await?;
----
if you want to commit your changes.
--

Studio::
+
--
Select your database using the database selector (image:{page-component-version}@home::studio-icons/database-none.png[width=128]), then select your transaction type in the top menu.

Open your query file. Then click the run button to run your query. Finally, use the commit button (image:{page-version}@home::studio-icons/svg/studio_check.svg[width=24]) to persist any changes made.
--
====

Do you take a hands-on approach to learning? If so, try running queries in this course in the following order:

. Define a schema as outlined in the <<schema,schema section>>. Use `schema` transactions for your queries and don't forget to commit any changes made.

. Insert data instances into the types of your schema as explained in the <<CRUD,CRUD section>>. Use `write` transactions for your queries and don't forget to commit any changes you want to persists.

. Read your data using query pipelines like those in the <<read,pipeline section>>.


== Beyond the basics: functions and query composition

// schema functions vs helper functions
// more examples
// match P; match Q; vs match P

[NOTE]
====
Coming soon.
====

== What next?

Continue learning how to use TypeDB with TypeDB Academy, or explore other sections of the documentation.

[cols-2]
--
.xref:{page-version}@academy::index.adoc[]
[.clickable]
****
An end-to-end learning experience for TypeDB and TypeQL, showing how to take advantage of TypeDB's unique features.
****

.xref:{page-version}@manual::index.adoc[TypeDB manual]
[.clickable]
****
Practice-oriented guides on using TypeDB, including the TypeDB Studio and TypeDB Console manuals.
****

.xref:{page-version}@drivers::index.adoc[TypeDB drivers]
[.clickable]
****
Installation guides, tutorials, and API references for the official TypeDB drivers in all supported languages.
****

.xref:{page-version}@typeql::index.adoc[TypeQL reference]
[.clickable]
****
Complete language reference for TypeQL, covering all query types, pattern elements, and keywords.
****
--
