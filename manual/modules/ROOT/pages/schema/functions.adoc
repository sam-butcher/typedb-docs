= Defining functions
:test-tql: linear

This page explains how to define functions within TypeDB schemas.

== Overview

include::{page-version}@typeql::functions/index.adoc[tag=description]

Functions can be defined in two ways:

1. We can define functions with "`globally`" in the schema of your database. Functions defined in the schema can be used in any data pipeline.
1. Or, we can define functions "`locally`" as part of a specific data pipeline. Such functions can only be called from within their parent pipeline.

The function definition syntax in both cases is identical. This page focuses on defining functions in schemas. For local functions in data pipeline see the xref:{page-version}@typeql::pipelines/with.adoc[with stage] reference.

== Defining functions

Similar to xref:{page-version}@manual::schema/types.adoc[type definitions], functions can be stored in a database schema using `schema` xref:{page-version}@manual::queries/transactions.adoc[transactions].
Three types of schema definition queries are available for working with functions:

* *Defining a new function*:
+
--
[,typeql]
----
define <function_name> <function_signature>:
  <function_body>
----
--

* *Undefining an existing function*:
+
--
[,typeql]
----
undefine <function_name>;
----
--

* *Modifying an existing definition*:
+
--
[,typeql]
----
redefine <function_name> <function_signature>:
  <function_body>
----
--

=== Function signatures

Function signatures determine:

* The tuple of typed input variables, e.g., `$x : user, $y : string, $z : bool` (these variables can then be used in the function body)
* The tuple of output types, e.g. `post, integer, integer`, which can either be a *stream* of output tuples (indicated by surrounding brackets `{ ... }`) or a *scalar* tuple, meaning the function return _at most_ one tuple.

.Example signatures
[,typeql]
----
( $x : user, $y : string, $z : bool ) -> post, integer, integer      # scalar output
( $x : user, $y : string, $z : bool ) -> { post, integer, integer }  # stream output
( ) -> integer                                                       # empty input
----

=== Function body

The function body comprises

- A TypeQL xref:{page-version}@typeql::pipelines/index.adoc[read pipeline] (which may have multiple stages, like `match`, `sort`, `reduce`)
- A *return statement* starting with the keyword `return`

For xref:{page-version}@typeql::functions/stream.adoc[stream functions], `return` will be followed simply by a `<tuple>` of variables that are to be returned.

For xref:{page-version}@typeql::functions/scalar.adoc[scalar functions], the `return` statements will contain some sort of reduction of the answer set to (zero or) one answer:

- `return first <tuple>` returns the first answer only, and similarly, `return last ...` returns the last.
- `return <aggr>, ...` returns a built-in aggregation result (like sums, counts, means, ...)


== Examples

=== Computing values

[#_stream_arithmetic_example]
The following function match `$z` to be the sum of two integer arguments `$x` and `$y`.

//!program[lang=tql, type=schema]
//!++
[,typeql]
.Stream function definition
----
define
  fun add_streamed($x: integer, $y: integer) -> { integer }:
    match
      let $z = $x + $y;
    return { $z };
----
//!--
//!run

If you are certain that `$z` will only match a single value (or if only one value is needed), the `first` or `last` keywords can be used to ensure the function returns a xref:{page-version}@typeql::functions/scalar.adoc[scalar result].

//!program[lang=tql, type=schema]
//!++
.Scalar function definition
[,typeql]
----
define
  fun add($x: integer, $y: integer) -> integer:
    match
      let $z = $x + $y;
    return first $z;
----
//!--
//!run

=== Querying for data instances

[#_stream_example]
.Stream function definition returning data instances
//!program[lang=tql, type=schema]
////
define
  attribute phone, value string;
  entity user, owns phone;
////
//!++
[,typeql]
----
define
  fun user_phones($user: user) -> { phone }:
    match
      $user has phone $phone;
    return { $phone };
----
//!--
//!run

=== Functions within functions

.Tuple function definition with inner function calls
//!program[lang=tql, type=schema, name=cast_issue]
////
define
  attribute karma @independent, value double;
////
// TODO: this example needs fixing (will fix later examples)
//!++
[,typeql]
----
define
  fun square($x: double) -> double:
    match
      let $z = $x * $x;
    return first $z;
  fun karma_sum_and_sum_squared() -> double, double:
    match
      $karma isa karma;
      let $karma-squared = square($karma);
    return sum($karma), sum($karma-squared);
----
//!--
//!run

[NOTE]
====
The output type here is not a stream because each tuple component is derived from an aggregation, which produces a single result.
====

Refer to <<using_functions>> for more details on calling functions.

== Modifying functions

=== Function undefinition

To undefine a function from a schema, you only need its name, which uniquely identifies it.
The following query removes <<_stream_arithmetic_example, a function defined earlier>>:

//!program[lang=tql, type=schema]
//!++
[,typeql]
----
undefine
  fun add_streamed;
----
//!--
//!run

=== Function redefinition

To redefine an existing function, use the same `define` syntax with the `redefine` keyword.
The query below updates <<_stream_example, a previously defined function>> to accept a `username` instead of a specific `user`:

//!program[lang=tql, type=schema, rollback=yes]
////
define attribute username, value string; user owns username;
////
//!++
[#_redefine_example]
[,typeql]
----
redefine
  fun user_phones($username-value: string) -> { phone }:
    match
      $user isa user, has username $username, has phone $phone;
      $username == $username-value;
    return { $phone };
----
//!--
//!run

[NOTE]
====
You can only redefine existing functions.
The function name must remain the same.
To change the name, define a new function and undefine the old one.
====

[#using_functions]
== Calling functions from patterns

Functions can be called in queries as other xref:{page-version}@typeql::statements/index.adoc[statements].
As shown in previous examples, to call a function, reference its name and pass the required arguments (if applicable) within parentheses.

For functions returning a single result, use a simple assignment to bind the output variable to the function's result:

//!program[lang=tql, type=read]
//!++
[,typeql]
----
match
  let $answer = add(2016, 9);
----
//!--
//!run

[NOTE]
====
The xref:{page-version}@typeql::statements/let-eq.adoc[`let` keyword] is required for value assignment in this example.
====

For functions returning a stream, use xref:{page-version}@typeql::statements/let-in.adoc[`let ... in` statements] to bind variables:

//!program[lang=tql, type=read]
//!++
[,typeql]
----
match
  $u isa user;
  let $phone in user_phones($u);
----
//!--
//!run

To handle tuple results, bind the appropriate number of variables to unpack the functionâ€™s output:

//!program[lang=tql, type=read, name=i_fail_bc_of_earlier_failure]
//!++
[,typeql]
----
match
  let $karma, $karma-squared in karma_sum_and_sum_squared();
----
//!--
//!run

For further details, refer to the xref:{page-version}@typeql::functions/index.adoc[TypeQL documentation].

[IMPORTANT]
====
The arguments in a function call are *positional* (i.e., arguments in the function call in a pattern are lined up with function arguments in its defined function signature).
Assigning a function output tuples to variables in a patterns is also positional.
====
