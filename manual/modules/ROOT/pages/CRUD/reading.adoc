= Reading data
:test-tql: linear

//!program[lang=tql, type=schema]
////
define
  entity user;
  attribute email, value string;
  attribute username, value string;
  user owns email, owns username;
  relation friendship, relates friend @card(0..2);
  user plays friendship:friend;
////
////
insert
  $a isa user, has username "A", has email "A@gmail.com";
  $b isa user, has username "B", has email "B@gmail.com";
  $c isa user, has username "C", has email "C@gmail.com";
  $d isa user, has username "D", has email "D@gmail.com";
  friendship($a, $b);
  friendship($c, $b);
////
//!run

This page explains how to read data from a TypeDB database.

== Overview

A read query in TypeDB is also called a *read pipeline*. A read pipeline may comprise multiple *stages*, that retrieve and operate on data.

* A `match` stage retrieves data based on a supplied pattern.
* Stages like `sort`, `reduce`, `limit`, ... operate on data.

A read pipeline may comprise different combinations of stages (refer to the xref:{page-version}@typeql::pipelines/index.adoc[] for a full list). It should always start with a `match` stage (not counting any preamble `with` stages used to make function definitions).

Most of the read stages produce concept rows and also take concept rows as inputs: this allows stages to be chained together, with the output of one stage being fed as input to the next stage. The only exception is <<_fetch, fetch>>, which produces JSON documents, and can therefore only appear at the end of a pipeline.

Read pipelines can be executed in any of the `schema`, `write`, or `read` xref:{page-version}@manual::queries/transactions.adoc[transaction kinds].

== Using match: the basics

TypeQL queries retrieve data by matching *patterns*. Patterns comprise one or more statements containing variables. Match stages return _all combinations_ of data for their variables that satisfy the pattern (note that this only applies to "`free`" variables, i.e. those which are not bound by previous stages).

[NOTE]
====
Patterns can also be combined into larger patterns by conjunction with `;`, disjuction with `or`, and negation with `not`. See the xref:{page-version}@typeql::patterns/index.adoc[pattern reference] for more.
====

When retrieving data from the database there is a basic distinction to keep in mind:

* *objects*, i.e. entities and relations, do not have a value that can be returned from the database; instead only their database-intrinstic representation, xref:{page-version}@typeql::data_model.adoc#representation[IID], is returned to the client.
* *attributes* have both an IID and a (human readable) *value* that can be returned to the client.

In the examples below, both cases will be illustrated.

[#_read_instance]
=== Reading objects

To read type instances (an xref:{page-version}@typeql::statements/entity.adoc[entity], a xref:{page-version}@typeql::statements/relation.adoc[relation], or an xref:{page-version}@typeql::statements/attribute.adoc[attribute]), use the xref:{page-version}@typeql::statements/isa.adoc[`isa` keyword]:

//!program[lang=tql, type=read]
//!++
.Users reading example
[,typeql]
----
match
  $u isa user;
----
//!--
//!run

This simple query will return all the `user` s in the database.

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $u | iid 0x1e00030000000000000002 isa user
   --------
    $u | iid 0x1e00030000000000000003 isa user
   --------
    $u | iid 0x1e00030000000000000004 isa user
   --------
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

Combining statements, it is possible to add more constraints to the `match`.
For example, the following query searches for all `user` s with a specific `username` using the xref:{page-version}@typeql::statements/has.adoc[`has` statement] (which can be interpreted as "the `user` with a specific `username` " as this attribute is a xref:{page-version}@typeql::annotations/key.adoc[key]):

//!program[lang=tql, type=read]
//!++
[#_specific_users_example]
.Specific users reading example
[,typeql]
----
match
  $u isa user, has username "User";
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

Similar to entity objects, we may also query for relation

//!program[lang=tql, type=read]
//!++
.Matching relation linking two instances with specific roles example
[,typeql]
----
match
  $a isa user, has username "Alice", has email "alice@typedb.com";
  $b isa user, has username "Bob", has email "bob@typedb.com";
  $f isa friendship, links (friend: $a, friend: $b);
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $a | iid 0x1e00030000000000000002 isa user
    $b | iid 0x1e00030000000000000003 isa user
    $f | iid 0x1f00020000000000000000 isa friendship
   --------
----
====

If you are sure that these two `user` s can only play specific roles in a `friendship` or your intent is to query for any roles these `user`s play in a relation, the query can be simplified:

//!program[lang=tql, type=read]
//!++
.Matching relation linking two instances of any roles example
[,typeql]
----
match
  $a isa user, has username "Alice", has email "alice@typedb.com";
  $b isa user, has username "Bob", has email "bob@typedb.com";
  $f isa friendship, links (friend: $a, friend: $b);
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $a | iid 0x1e00030000000000000002 isa user
    $b | iid 0x1e00030000000000000003 isa user
    $f | iid 0x1f00020000000000000000 isa friendship
   --------
----
====

TypeDB and TypeQL also support further simplification in case you are interested in only the `user` s, so the `friendship` between them is not needed in the output:

//!program[lang=tql, type=read]
//!++
[#_matching_relation_linking_without_var_example]
.Matching relation linking two instances without a variable example
[,typeql]
----
match
  $a isa user, has username "Alice", has email "alice@typedb.com";
  $b isa user, has username "Bob", has email "bob@typedb.com";
  friendship ($a, $b);
----
//!--
//!run

[NOTE]
====
This is equivalent to using anonymous variable usage:

//!program[lang=tql, type=read]
//!++
.Matching relation linking two instances with an anonymous variable example
[,typeql]
----
match
  $a isa user, has username "Alice", has email "alice@typedb.com";
  $b isa user, has username "Bob", has email "bob@typedb.com";
  $_ isa friendship, links ($a, $b);
----
//!--
//!run
====

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $a | iid 0x1e00030000000000000002 isa user
    $b | iid 0x1e00030000000000000003 isa user
   --------
----
====

=== Reading attributes and values

Attributes and their values can be read in a similar way.
By variablizing the attribute part of the <<_specific_users_example, previous example>>, it's easy to get all `user` s and their `username` s.
You can also use anonymous variables (`$_`) if they are not needed in the output:

//!program[lang=tql, type=read]
//!++
[#_specific_users_example]
.Matching all users' usernames example
[,typeql]
----
match
  $_ isa user, has username $n;
----
//!--
//!run

[NOTE]
====
Equivalently, we could query

//!program[lang=tql, type=read]
//!++
.Matching all usernames
[,typeql]
----
match
  $n isa username;
----
//!--
//!run

(The queries may only differ in the case of independent attributes, as these may not be owned, and ownership is required by the first version of the query.)
====

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $n | Alice isa username
   --------
    $n | Bob isa username
   --------
    $n | Charlie isa username
   --------
    $n | User isa username
   --------
----
====

While the attribute is variablized, it is also possible to put the same constraints from the <<_specific_users_example, specific user search example>> on it.
For instance, we can use the same attribute variable twice in two compatible statements:

//!program[lang=tql, type=read]
//!++
.Specific users reading with variablized attribute
[,typeql]
----
match
  $n isa username "User";
  $u isa user, has username $n;
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $n | User isa username
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

To achieve the same result, we could also compare the attribute variable's value with the used constant:

//!program[lang=tql, type=read]
//!++
.Specific users reading with variablized attribute and value comparison
[,typeql]
----
match
  $n isa username;
  $n == "User";
  $u isa user, has username $n;
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $n | User isa username
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

Alternatively, we could declare a value variable using the xref:{page-version}@typeql::statements/let-eq.adoc[`let` statement], and integrate comparison into the owner instance declaration:

//!program[lang=tql, type=read]
//!++
.Specific users reading using value variables example
[,typeql]
----
match
  let $n = "User";
  $u isa user, has username == $n;
----
//!--
//!run

[NOTE]
====
Here, `$n` will be a value variable, not an attribute variable, so it's cleaner to compare with it instead of binding from the previous examples.
Try comparing the outputs of this and the previous examples.
====

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $n | User
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

=== Reading type labels

Types from database schemas are also available for `match` ing and retrieving.
To bind a variable to a type, use type-related statements and variablize the respective side.
The following example query retrieves the root types (types without supertypes) of the owners of attributes of type labelled `email`, using the xref:{page-version}@typeql::statements/isa.adoc[`isa` statement], the xref:{page-version}@typeql::statements/label.adoc[`label` statement], and the xref:{page-version}@typeql::statements/sub.adoc[`sub!` statement]:

//!program[lang=tql, type=read]
//!++
.Matching types example
[,typeql]
----
match
  $_ isa $owner-type, has $attribute-type $_;
  $attribute-type label email;
  not { $owner-type sub! $_; };
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   ---------------------
    $attribute-type | type email
    $owner-type     | type content
   ---------------------
----
====

Refer to the xref:{page-version}@typeql::pipelines/match.adoc[] page for a detailed explanation and additional examples.
A full list of statements, including the ones available for `match` ing, is available xref:{page-version}@typeql::statements/index.adoc[here].

== Matching composite patterns

In the examples above we have already seen the use of `not` subpatterns. This pattern constructor, together with the disjunction constructor `or`, provide powerful ways to combine patterns.

//!program[lang=tql, type=read]
//!++
.Matching with disjunction example
[,typeql]
----
match
  $u isa user;
  { $u has username "User"; } or { $u has email "alice@typedb.com"; };
----
//!--
//!run

[NOTE]
====
Note that multiple semicolons are required: one semicolon for each single statement and a final semicolon to finalize the disjunction.
====

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $u | iid 0x1e00030000000000000002 isa user
   --------
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

Similarly, xref:{page-version}@typeql::patterns/negations.adoc[negations] (logical "NOT") to reverse the declared constraint.
The following example returns all `user` s without `email` s:

//!program[lang=tql, type=read]
//!++
.Matching with negation example
[,typeql]
----
match
  $u isa user;
  not { $u has email $_; };
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $u | iid 0x1e00030000000000000005 isa user
   --------
----
====

See xref:{page-version}@typeql::patterns/index.adoc[] for more details about patterns.

== Pipelining stages

Let's now discuss how to use the matched results in pipeline stages.

For example, it is frequently useful to get only a specific set of variables instead of everything that is matched in the constraint declarations.
To select the necessary variables from the <<_matching_relation_linking_without_var_example, previous example>>, use the xref:{page-version}@typeql::pipelines/select.adoc[select operator]:

//!program[lang=tql, type=read]
//!++
.Selecting variables example
[,typeql]
----
match
  $a isa user, has username "Alice", has email "alice@typedb.com";
  $b isa user, has username "Bob", has email "bob@typedb.com";
  $f isa friendship, links (friend: $a, friend: $b);
select $f; # or reversed: "select $a, $b;"
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $f | iid 0x1f00020000000000000000 isa friendship
   --------
----
====

To sort the outputs by chosen variables, use the xref:{page-version}@typeql::pipelines/sort.adoc[sort operator]:

//!program[lang=tql, type=read]
//!++
.Sorting results in descending order example
[,typeql]
----
match
 $u isa user, has username $n;
sort $n desc;
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
   --------
    $n | User isa username
    $u | iid 0x1e00030000000000000005 isa user
   --------
    $n | Charlie isa username
    $u | iid 0x1e00030000000000000004 isa user
   --------
    $n | Bob isa username
    $u | iid 0x1e00030000000000000003 isa user
   --------
    $n | Alice isa username
    $u | iid 0x1e00030000000000000002 isa user
   --------
----
====

As the mentioned operators can reuse the variables from previous pipeline stages and produce their own results, it is a common approach to combine multiple stages in complex pipelines.
Continue exploring pipelines in the xref:{page-version}@manual::CRUD/pipelines.adoc[] manual.

The above example only cover simple 2-stage pipelines; in theory, there are now boundaries set for how you may combine stages. See xref:{page-version}@manual::CRUD/pipelines.adoc[] for more examples.

[#_fetch]
== Converting output to JSON documents

Results of read pipeline stages in TypeDB can be converted into documents of the JSON format using `fetch`.
The main purpose is to export the data in a format that's easily readable by other appplications.
But `fetch` not only allows you to flexibly structure your output based on variables, but also to perform subquerying operations, xref:{page-version}@manual::schema/functions.adoc[function] calls, reference instance's attributes, and much more.

[NOTE]
====
You can use `fetch` after any stage producing rows, even if you xref:{page-version}@manual::CRUD/inserting.adoc[insert] or xref:{page-version}@manual::CRUD/deleting.adoc[delete] data.
====

Fetch receives a number of input rows and converts each row into a document of the provided structure, adding more requested information based on the query.

For example, you can simply label all the matched results and address their attributes (if applicable):

//!program[lang=tql, type=read]
//!++
.Creating a document of users example
[,typeql]
----
match
  $u isa user, has username $n;
fetch {
  "user email": $u.email,
  "matched username": $n
};
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
{
    "matched username": "Alice",
    "user email": "alice@typedb.com"
}
{
    "matched username": "Bob",
    "user email": "bob@typedb.com"
}
{
    "matched username": "Charlie",
    "user email": "charlie@typedb.com"
}
{
    "matched username": "User",
    "user email": null
}
----
====

Note the body of the `fetch` stage "`declares`" the structure of the output documents.

[NOTE]
====
The example above shows the difference between matching using attributes and accessing attributes.
Notice that the output of the previous `fetch` query contains `null`: it happens because the first `match` stage returned all `user` s with `username` s, and `fetch` had to process every incoming row without changing the number of answers, even if the request cannot be satisfied.

However, the query execution would return an error if the `fetch` stage was completely unsatisfiable.
To achieve this, you can try accessing attributes of an attribute.
====

The following is an example of document nesting and JSON list usage.
We also produce an additional document containing every existing attribute of the instance using `.*`.

//!program[lang=tql, type=read]
//!++
.Creating a document of users with nesting and lists example
[,typeql]
----
match
  $u isa user;
fetch {
  "user information": {
    "username": $u.username,
    "emails": [ $u.email ]
  },
  "all user attributes": { $u.* }
};
----
//!--
//!run

.Example TypeDB Console output
[%collapsible]
====
----
{
    "all user attributes": {
        "email": "alice@typedb.com",
        "username": "Alice"
    },
    "user information": {
        "emails": [ "alice@typedb.com" ],
        "username": "Alice"
    }
}
{
    "all user attributes": {
        "email": "bob@typedb.com",
        "username": "Bob"
    },
    "user information": {
        "emails": [ "bob@typedb.com" ],
        "username": "Bob"
    }
}
{
    "all user attributes": {
        "email": "charlie@typedb.com",
        "username": "Charlie"
    },
    "user information": {
        "emails": [ "charlie@typedb.com" ],
        "username": "Charlie"
    }
}
{
    "all user attributes": { "username": "User" },
    "user information": {
        "emails": [  ],
        "username": "User"
    }
}
----
====

[NOTE]
====
Note how the missing `email` is represented as an empty list: wrapping empty results in lists produces empty lists `[ ]` instead of JSON ``null``s.

The `.*` command matches only existing attributes, thus, does not mention `null` s.
====

Refer to xref:{page-version}@typeql::pipelines/fetch.adoc[] to explore all applications of `fetch`, its syntax, and more examples with subqueries, functions, and much more.

== Having troubles?

include::{page-version}@manual::page$CRUD/inserting.adoc[tag=having-troubles]
