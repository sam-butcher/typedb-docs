= Define query
:page-aliases: {page-version}@new_reference::typeql/pages/queries/define.adoc

Define queries are used
// tag::overview[]
to add new types, constraints, or functions to the schema.
// end::overview[]

== Syntax

Define queries start with the `define` keyword following by definition statements.

[,typeql]
----
define
  <definition_statements>
----

Definition statement can consist of any number of schema-related xref:{page-version}@new_reference::typeql/pages/statements/index.adoc[statements], simple or composite. Statements should be separated by semicolons.
Schema statements in a define query cannot use variables or values, except for functions.

== Behavior

A define query adds schema statements to an existing schema. These statements can add new types and functions, or they can add new traits and annotations to existing types. Define queries cannot change schema statements already existing in a schema.

Define queries are idempotent, meaning they can be applied multiple times without changing the result of the initial application.

An error is returned (on execution or on commit), and no changes are preserved, if:

- A definition conflicts with an existing definition.
- The query results in an invalid schema.

[#_statements_behavior]
=== Statements

* xref:{page-version}@new_reference::typeql/pages/statements/entity.adoc[`entity` statements] define new entity types. This statement should be provided for every new type of kind entity.
* xref:{page-version}@new_reference::typeql/pages/statements/relation.adoc[`relation` statements] define new relation types. This statement should be provided for every new type of kind relation.
* xref:{page-version}@new_reference::typeql/pages/statements/attribute.adoc[`attribute` statements] define new attribute types. This statement should be provided for every new type of kind attribute.
* xref:{page-version}@new_reference::typeql/pages/statements/relates.adoc[`relates` statements] define new role types for relations. It can be followed by the `as` keyword to define a specialization for two role types.
* xref:{page-version}@new_reference::typeql/pages/statements/plays.adoc[`plays` statements] define new roleplayings between types and relation types. Only entity and relation types can play roles.
* xref:{page-version}@new_reference::typeql/pages/statements/owns.adoc[`owns` statements] define new ownerships between types and attribute types. Only entity and relation types can own attributes.
* xref:{page-version}@new_reference::typeql/pages/statements/value.adoc[`value` statements] define new value types of attribute types. An attribute type can only have up to one value type. If another `value` is already defined for the subject type, an error is returned.
* xref:{page-version}@new_reference::typeql/pages/statements/sub.adoc[`sub` statements] define new subtyping between two types. Those types should be of the same kind: entity, relation, or attribute. If another `sub` is already defined for the subject type, an error is returned.
* xref:{page-version}@new_reference::typeql/pages/annotations/index.adoc[@annotations] define new annotations. If another annotation of the same type but with different arguments is already defined, an error is returned. Can be applied to:
- Type labels for entities, relations, attributes.
- Traits: `relates`, `plays`, `owns`.
- Attribute value types: `value`.
* xref:{page-version}@new_reference::typeql/pages/statements/fun.adoc[`fun` statements] define new functions.

== Examples

=== Defining types

.Defining entity types
[,typeql]
----
define entity user;
----

.Defining relation types
[,typeql]
----
define relation parentship;
----

.Defining attribute types
[,typeql]
----
define attribute email;
----

=== Defining traits

.Defining relation type's relates
[,typeql]
----
define parentship relates parent, relates child;
----

.Defining relation types with subtyping
[,typeql]
----
define relation fathership sub parentship;
----

.Defining relates with role type specializing
[,typeql]
----
define fathership relates father as parent;
----

.Defining roleplaying for multiple roles
[,typeql]
----
define user plays parentship:parent, plays parentship:child;
----

.Defining attribute type's value type
[,typeql]
----
define email value string;
----

.Defining type's ownership
[,typeql]
----
define user owns email;
----

=== Defining annotations

.Defining type's annotation without arguments
[,typeql]
----
define parentship @abstract;
----

.Defining attribute type's annotation and value type's annotation with arguments
[,typeql]
----
define email @independent, value string @regex("^.*@\w+\.\w+$");
----

.Defining type's ownership's annotation with arguments
[,typeql]
----
define user owns email @card(0..2);
----

.Defining annotations for specializing relates
[,typeql]
----
define fathership relates father as parent @abstract;
----

=== Defining functions

include::{page-version}@new_reference::typeql/pages/functions/writing.adoc[tag=define]
