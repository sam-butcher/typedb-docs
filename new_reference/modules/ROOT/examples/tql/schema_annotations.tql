// tag::define-keyword[]
define
// end::define-keyword[]
// tag::abstract-entity[]
  entity element @abstract;
// end::abstract-entity[]

  entity content @abstract, sub root,
    owns is-visible,
    plays subscription:content;

// tag::key[]
  entity content owns id @key;
// end::key[]

  entity page @abstract, sub content,
    owns page-id,
    owns bio,
    owns profile-picture,
    owns badge,
    plays posting:page,
    plays viewing:viewed,
    plays following:page;

// tag::card-owns-13[]
  entity page owns name @card(1..3);
// end::card-owns-13[]

// tag::card-plays-0inf[]
  page plays posting:page @card(0..);
// end::card-plays-0inf[]

  entity profile @abstract,
    owns username,
    plays location:located,
    plays viewing:viewer,
    plays content-engagement:author,
    plays following:follower,
    plays subscription:subscriber;

// tag::profile-sub-page[]
  entity profile sub page;
// end::profile-sub-page[]

  entity user,
    owns gender,
    owns language,
    owns email,
    plays friendship:friend,
    plays family:relative,
    plays parentship:parent,
    plays parentship:child,
    plays siblingship:sibling;

// tag::regex-owns[]
  entity user owns phone @regex("^\d{10,15}$");
// end::regex-owns[]
// tag::unique[]
  entity user owns phone @unique;
// end::unique[]

// tag::unique-sub-entity[]
  entity uk-user sub user, owns uk-phone;
  entity usa-user sub user, owns usa-phone;
// end::unique-sub-entity[]

// tag::user-owns-name[]
  entity user sub profile,
    owns first-name @card(1),
    owns surname @card(0..);
// end::user-owns-name[]

  relation social-relation @abstract,
    relates related @card(0..);

  relation friendship sub social-relation,
    relates friend as related @card(0..);

  relation family sub social-relation,
    relates relative as related @card(0..1000);

  relation parentship sub family,
    relates parent @card(1..2),
    relates child @card(1..);

  relation siblingship sub family,
    relates sibling @card(2..);

  entity post @abstract, sub content,
    owns post-id,
    owns post-text,
    owns language,
    owns tag @card(0..10),
    owns post-date,
    plays posting:post,
    plays commenting:parent,
    plays reaction:parent,
    plays location:located,
    plays viewing:viewed;

// tag::range-owns[]
  entity post owns creation-timestamp @range(1970-01-01T00:00:00..);
// end::range-owns[]

  entity text-post sub post;

  entity image-post sub post,
    owns post-image;

  entity video-post sub post,
    owns post-video;

  entity live-video-post sub video-post;

  entity comment sub content,
    owns comment-id,
    owns comment-text,
    owns creation-timestamp,
    owns tag @card(0..5),
    plays commenting:comment,
    plays commenting:parent,
    plays reaction:parent;

// tag::abstract-relation[]
  relation interaction @abstract,
    relates subject @abstract,
    relates content;

  relation viewing sub interaction,
    relates viewer as subject,
    relates viewed as content;
// end::abstract-relation[]

  relation content-engagement @abstract, sub interaction,
    relates author as subject;

  relation posting sub content-engagement,
    relates page as content,
    relates post;

// tag::card-relates-01k[]
  relation posting relates page @card(0..1000);
// end::card-relates-01k[]

  relation commenting sub content-engagement,
    relates parent as content,
    relates comment;

  relation reaction sub content-engagement,
    relates parent as content,
    owns creation-timestamp;

// tag::values-owns[]
  relation reaction owns emoji @values("like", "love", "funny", "surprise", "sad", "angry");
// end::values-owns[]

  relation following,
    relates follower,
    relates page;

  relation subscription,
    relates subscriber,
    relates content;

// tag::abstract-attribute[]
  attribute id @abstract, value string;
  attribute page-id @abstract, sub id;
  attribute username sub page-id;
// end::abstract-attribute[]
// tag::key-attribute[]
  attribute id value string;
// end::key-attribute[]
  attribute post-id sub id;
  attribute comment-id sub id;

// tag::name-attribute[]
  attribute name value string;
// end::name-attribute[]
// tag::name-subattributes[]
  attribute name @abstract, value string;
  attribute first-name sub name;
  attribute surname sub name;
// end::name-subattributes[]
// tag::values-value[]
  attribute gender value string @values("male", "female", "other");
// end::values-value[]
// tag::independent-attribute[]
  attribute language @independent, value string;
// end::independent-attribute[]
// tag::regex-value[]
  attribute email value string @regex("^.*@\w+\.\w+$");
// end::regex-value[]
// tag::phone-attribute[]
  attribute phone value string;
// end::phone-attribute[]
// tag::phone-sub-attributes[]
  phone @abstract;
  attribute uk-phone sub phone;
  attribute usa-phone sub phone;
// end::phone-sub-attributes[]

  attribute payload @abstract, value string;
  attribute text-payload @abstract, sub payload;
  attribute media-payload @abstract, sub payload;
  attribute image-payload @abstract, sub media-payload;
  attribute video-payload @abstract, sub media-payload;
  attribute bio sub text-payload;
  attribute profile-picture sub image-payload;
  attribute post-text sub text-payload;
  attribute post-image sub image-payload;
  attribute post-video sub video-payload;
  attribute comment-text sub text-payload;

# TODO: Add the following lines to schema_large and schema_small when the definition bug is fixed
#  latitude value double @range(-90.0..90.0);
#  longitude value double @range(-180.0..180.0);
// tag::range-value[]
  attribute latitude value double @range(-90.0..90.0);
// end::range-value[]

  attribute tag value string;
// tag::emoji-attribute[]
  attribute emoji value string;
// end::emoji-attribute[]
  attribute badge value string;

// tag::creation-timestamp-attribute[]
  attribute creation-timestamp, value datetime;
// end::creation-timestamp-attribute[]

  attribute is-visible, value boolean;
